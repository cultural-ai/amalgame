:-module(ag_opm, [
		  opm_was_generated_by/4,       % +Process (cause), +Artifact (effect), +RDFGraph, +Options
		  opm_include_dependency/2,     % +SourceGraph, +TargetGraph
		  opm_clear_process/1,           % +Process (bnode)
		  opm_assert_artefact_version/3
		 ]).

/* <module> OPM -- simple support for the OPM Provenance Model (OPM)

@see http://openprovenance.org/

*/

:- use_module(library(http/http_host)).
:- use_module(library(semweb/rdf_db)).
:- use_module(library(semweb/rdfs)).
:- use_module(library(version)).
:- use_module(library(opmv_schema)).
:- use_module(library(opmvc_schema)).
:- use_module(user(user_db)).
:- use_module(library(http/http_session)).

opm_include_dependency(Graph, Target) :-
	opm_include_dependency([Graph], [], DepList),
	expand_deplist(DepList, [], Results),
	rdf_assert_list(Results, Target).

rdf_assert_list([], _).
rdf_assert_list([rdf(S,P,O)|T], Graph) :-
	rdf_assert(S,P,O,Graph),
	rdf_assert_list(T, Graph).

opm_include_dependency([], Results, Results).

opm_include_dependency([H|T], Accum, Results) :-
	opm_include_dependency(T, Accum, TailResults),
	findall(Dep, dependent(H, Dep),Deps),
	opm_include_dependency(Deps, [H], HeadResults),
	append(TailResults, HeadResults, Results).

dependent(S, Dep) :- rdf(S, opmv:wasDerivedFrom, Dep).
dependent(S, Dep) :- rdf(S, opmv:wasGeneratedBy, Dep).
dependent(S, Dep) :-
	rdfs_individual_of(S, opmv:'Process'),
	rdf(S,_,Dep),
	rdf_is_bnode(Dep).

expand_deplist([], Results, Results).
expand_deplist([H|T], Accum, Results) :-
	findall(Triple, opm_triple(H,Triple), Triples),
	append(Triples, Accum, NewAccum),
	expand_deplist(T, NewAccum, Results).

:- rdf_meta
	opm_triple(r, t).

opm_triple(S, rdf(S, owl:versionInfo,O))     :- rdf(S, owl:versionInfo, O).
opm_triple(S, rdf(S, opmv:wasDerivedFrom,O)) :- rdf(S, opmv:wasDerivedFrom, O).
opm_triple(S, rdf(S, opmv:wasGeneratedBy,O)) :- rdf(S, opmv:wasGeneratedBy, O).
opm_triple(S, rdf(S,P,O)) :-
	rdfs_individual_of(S, opmv:'Process'),
	rdf(S,P,O).
opm_triple(S, rdf(S,P,O)) :-
	rdf_is_bnode(S),
	(   rdfs_individual_of(S, align:'Cell') -> gtrace; true),
	rdf(S,P,O).

%%     opm_was_generated_by(+Process, +Artifacts, +Graph, +Options) is
%%     det.
%
%	Assert OPM provenance information about Artifacts generated by
%	Process into named Graph (all three are URLs).
%	Options is a list of options, currently implemented options
%	include:
%
%	* was_derived_from([Sources]) to indicate the artifacts were
%	derived from the given list of source artifacts
%	* request(Request) to record information about the request URI
%	used in the web service to create Artifacts.

opm_was_generated_by(_, [], _, _) :- !.
opm_was_generated_by(Process, Artifacts, Graph, Options) :-
	is_list(Artifacts),!,
	rdf_assert(Process, rdf:type, opmv:'Process',	Graph),
	forall(member(Artifact, Artifacts),
	       (   rdf_assert(Artifact, rdf:type, opmv:'Artifact',    Graph),
		   rdf_assert(Artifact, opmv:wasGeneratedBy, Process, Graph)
	       )
	      ),
	opm_program(Graph, Program),
	opm_agent(Graph, Agent),
	get_xml_dateTime(TimeStamp),
	rdf_bnode(BN_TimeStamp),
	rdf_assert(BN_TimeStamp, rdf:type, time:'Instant',  Graph),
	rdf_assert(BN_TimeStamp, time:inXSDDateTime, literal(type(xsd:dateTime, TimeStamp)), Graph),
	rdf_assert(Process, opmv:wasStartedAt,   BN_TimeStamp , Graph),
	rdf_assert(Process, opmv:wasPerformedBy, Program, Graph),
	rdf_assert(Process, opmv:wasPerformedBy, Agent,   Graph),

	(   memberchk(was_derived_from(Sources), Options)
	->  forall(member(Source, Sources),
		   (   forall(member(Artifact, Artifacts),
			     rdf_assert(Artifact, opmv:wasDerivedFrom,  Source,  Graph)
			     ),
		       rdf_assert(Process, opmv:used, Source, Graph),
		       (   \+ rdfs_individual_of(Source, opmv:'Artifact')
		       ->  rdf_assert(Source, rdf:type,	opmv:'Artifact', Graph)
		       ;   true
		       )
		   )
		  )
	;   true
	),
	(   memberchk(request(Request), Options)
	->  http_current_host(Request, Hostname, Port, [global(true)]),
	    memberchk(request_uri(ReqURI), Request),
	    memberchk(protocol(Protocol), Request),
	    format(atom(ReqUsed), '~w://~w:~w~w', [Protocol,Hostname,Port,ReqURI]),
	    rdf_assert(Process, amalgame:request, ReqUsed, Graph)
	;   true
	),
	true.

opm_was_generated_by(Process, Artifact, Graph, Options) :-
	atom(Artifact),!,
	opm_was_generated_by(Process, [Artifact], Graph, Options).

opm_clear_process(Process) :-
	rdf_retractall(Process, _, _, _),
	rdf_retractall(_, _, Process, _).

opm_program(Graph, Program):-
	git_module_property('amalgame',   home_url(PackUrl)),
	git_module_property('amalgame',   version(AG_version)),
	git_module_property('ClioPatria', version(CP_version)),!,
	(  current_prolog_flag(version_git, PL_version)
	-> true
	;   current_prolog_flag(version, PL_version)
	),
	AG_git = 'http://eculture.cs.vu.nl/git/public/?p=econnect/amalgame.git&a=commit&h=',
	format(atom(Program), '~w~w', [AG_git, AG_version]),
	format(atom(AG), 'Amalgame: ~w', [AG_version]),
	format(atom(CP), 'ClioPatria: ~w', [CP_version]),
	format(atom(PL), 'SWI-Prolog: ~w', [PL_version]),
	rdf_assert(Program, rdf:type,	opmvc:'Program', Graph),
	rdf_assert(Program, rdfs:label, literal('Amalgame'), Graph),
	rdf_assert(Program, opmvc:software, PackUrl, Graph),
	rdf_assert(Program, opmvc:software, literal(AG), Graph),
	rdf_assert(Program, opmvc:software, literal(CP), Graph),
	rdf_assert(Program, opmvc:software, literal(PL), Graph).

opm_agent(Graph, Agent) :-
	(
	http_in_session(_)
	->
	   logged_on(User, anonymous),
	   user_property(User, url(Agent)),
	   (   user_property(User, realname(UserName))
	   ->  true
	   ;   user_property(User, openid(UserName))
	   ->  true
	   ;   UserName = Agent
	   )
	;
	 rdf_bnode(Agent),
	 UserName = 'software_api'
	),

	rdf_assert(Agent, rdfs:label, literal(UserName),  Graph),
	rdf_assert(Agent, rdf:type,   opmv:'Agent',	  Graph).

get_xml_dateTime(TimeStamp) :-
	get_time(T),
	format_time(atom(TimeStamp), '%Y-%m-%dT%H-%M-%S%Oz', T).

%%	opm_assert_artefact_version(+Artifact,+SourceGraph,+TargetGraph) is semidet.
%
%	Assert (git) version information about Artifact into the named
%	graph TargetGraph. SourceGraph is the main named graph in which
%	Artifact is defined.

opm_assert_artefact_version(Artifact, SourceGraph, TargetGraph) :-
	rdf_graph_property(SourceGraph, source(SourceFileURL)),
	uri_file_name(SourceFileURL, Filename),
	file_directory_name(Filename, Dirname),
	register_git_module(Artifact, [directory(Dirname), home_url(Artifact)]),
	(   git_module_property(Artifact, version(Version))
	->  format(atom(VersionS),  'GIT version: ~w', [Version]),
	    rdf_assert(Artifact, owl:versionInfo, literal(VersionS), TargetGraph)
	;   (rdf_graph_property(SourceGraph, hash(Hash)),
	     rdf_graph_property(SourceGraph, source_last_modified(LastModified)),
	     format_time(atom(Mod), 'Last-Modified: %Y-%m-%dT%H-%M-%S%Oz', LastModified),
	     rdf_assert(Artifact, owl:versionInfo, literal(Mod), TargetGraph),
	     rdf_assert(Artifact, owl:versionInfo, literal(Hash), TargetGraph)
	    )
	).
