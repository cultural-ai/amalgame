:- module(align_stats, []). % No exports, HTTP entry points only

:- use_module(library(http/http_dispatch)).
:- use_module(library(http/http_parameters)).
:- use_module(library(http/html_write)).
:- use_module(library(semweb/rdf_db)).

:- use_module(auth(user_db)).
:- use_module(components(label)).

:- use_module(amalgame(compare/overlap)).
:- use_module(amalgame(mappings/alignment)).

:- http_handler(amalgame(list_alignments),    http_list_alignments,     []).
:- http_handler(amalgame(list_alignment),     http_list_alignment,      []).
:- http_handler(amalgame(find_overlap),       http_list_overlap,        []).
:- http_handler(amalgame(clear_cache),        http_clear_cache,         []).
:- http_handler(amalgame(split_alignment),    http_split_alignment,     []).
:- http_handler(amalgame(compute_stats),      http_compute_stats,       []).

%%	http_list_alignments(+Request) is det.
%
%	HTTP handler returning list of all alignments in HTML.

http_list_alignments(_Request) :-
	style(Style),
	reply_html_page(cliopatria(default),
			[title('Alignments'),
			 Style
			],
			[ h4('Alignments in the RDF store'),
			  \show_alignments
			]).

%%	http_list_alignment(+Request) is det.
%
%	HTTP handler returning list of all alignments in HTML.

http_list_alignment(Request) :-
	style(Style),
	http_parameters(Request, [graph(Graph, [])]),
	reply_html_page(cliopatria(default),
			[title('Alignment'),
			 Style
			],
			[ h4('Alignment overview'),
			  \show_alignment_overview(Graph)
			]).

http_split_alignment(Request) :-
	http_parameters(Request,
			[graph(Graph, []),
			 condition(Condition, [])
			]),
	style(Style),
	split_alignment(Graph, Condition, OutGraphs),
	align_clear_stats(found),
	reply_html_page(cliopatria(default),
			[title('Alignment splitted'),
			 Style
			],
			[ h4('Alignment splitted'),
			  div([],OutGraphs)
			]).

http_compute_stats(Request) :-
	http_parameters(Request, [graph(all, [])]),
	findall(G, is_alignment_graph(G,_), Graphs),!,
	forall(member(G, Graphs),
	       (   align_ensure_stats(totalcount(G)),
		   align_ensure_stats(mapped(G)),
		   align_ensure_stats(source(G)),
		   align_ensure_stats(target(G))
	       )
	      ),
	http_redirect(moved, location_by_id(http_list_alignments), Request).

http_compute_stats(Request) :-
	http_parameters(Request,
			[graph(Graph, []),
			 stat(Stats, [list(atom)])
			]),
	forall(member(Stat, Stats),
	       (   Type =.. [Stat, Graph],
		   align_ensure_stats(Type)
	       )
	      ),
	http_redirect(moved, location_by_id(http_list_alignments), Request).

%%	http_list_overlap(+Request) is det.
%
%	HTTP handler generating a page with mapping overlap statistics.

http_list_overlap(_Request) :-
	style(Style),
	reply_html_page(cliopatria(default),
			[
			 title('Alignment overlap'),
			 Style
			],
			[
			 div([class(lfloat)],
			     [
			      h4('Alignments in the RDF store'),
			      \show_alignments
			     ]),
			 div([class(lfloat)],
			     [
			      h4('Alignment overlap'),
			      \show_overlap
			     ])
			]).

%%	http_clear_cache(+Request) is det.
%
%	Clears named graphs with cached amalgame results.
%	@tbd: authorisation

http_clear_cache(_Request) :-
	authorized(write(amalgame_cache, clear)),
	align_clear_stats(all),
	clear_overlaps,
	reply_html_page(cliopatria(default),
			title('Amalgame caches cleared'),
			[h3('Amalgame generated data have been cleared'),
			 p('The following data been cleared:'),
			 ul([
			     li('Amalgame alignment overlap named graphs'),
			     li('Amalgame alignment abbreviation nicknamesrdf'),
			     li('Amalgame statistics data')
			    ])
			]).

%%	style(-Style) is det.
%
%	Common style for HTML pages generated by this module
%
style(style(
	    [type('text/css')],
	    ['#aligntable { padding: .3%; border: solid grey} ',
	     '#nicktable  { padding: .3%; border: dashed grey; margin-left: 5% } ',
	     '#totals td  { border-top: solid grey; font-weight: bold } ',
	     '#finalrow td { border-top: solid #AAAAAA; } ',
	     '.lfloat { float: left; margin-right: 2% }'
	    ])
     ).


show_alignment_overview(Graph) -->
	{
	 align_ensure_stats(source(Graph)),
	 align_ensure_stats(target(Graph)),
	 align_ensure_stats(mapped(Graph)),

	 http_link_to_id(http_evaluator, [graph=Graph], EvalLink),
	 http_link_to_id(list_graph, [graph=Graph], URI),
	 http_link_to_id(http_split_alignment,
			 [graph=Graph, condition=sourceType], STLink),
	 http_link_to_id(http_split_alignment,
			 [graph=Graph, condition=targetType], TTLink),
	 rdf(Graph, amalgame:source, Source),
	 rdf(Graph, amalgame:target, Target),
	 rdf(Graph, amalgame:mappedSourceConcepts, literal(MSC)),
	 rdf(Graph, amalgame:mappedTargetConcepts, literal(MTC))
	},
	html([div(['Alignment graph: ',
		   a([href(URI)], Graph), ' ',
		   a([href(EvalLink)], '(evaluate)')
		 ]),
	      table([
		     tr([td('Source voc:'),
			 td(\rdf_link(Source)),
			 td(a([href(STLink)], 'split on source type')),
			 td([style('text-align:right')],[MSC])
			]),
		     tr([td('Target voc:'),
			 td(\rdf_link(Target)),
			 td(a([href(TTLink)], 'split on target type')),
			 td([style('text-align:right')],[MTC])
			])
		    ]
		   )
	     ]
	    ).

show_alignment(Graph) -->
	{
	 nickname(Graph, Nick),
	 http_link_to_id(http_list_alignment, [graph(Graph)], VLink)
	},
	html(a([href(VLink),title(Graph)],[Nick, ' '])).

show_graph(Graph) -->
	{
	 http_link_to_id(list_graph, [graph(Graph)], VLink)
	},
	html(a([href(VLink)],\turtle_label(Graph))).

show_countlist([], Total) -->
	html(tr([id(finalrow)],
		[td(''),
		 td([style('text-align: right')], Total),
		 td('Total (unique alignments)')
		])).

show_countlist([Count:O:Example|T], Number) -->
	{
	  NewNumber is Number + Count
	},
	html(tr([
		 td(\show_overlap_graphs(O)),
		 td([style('text-align: right')],Count),
		 \show_example(Example)
		])),
	show_countlist(T,NewNumber).

show_example([E1, E2]) -->
	{
	 atom(E1), atom(E2),
	 http_link_to_id(list_resource, [r(E1)], E1Link),
	 http_link_to_id(list_resource, [r(E2)], E2Link)
	},
	html([td(a([href(E1Link)],\turtle_label(E1))),
	      td(a([href(E2Link)],\turtle_label(E2)))]).

show_example([E1, E2]) -->
	html([td(E1),td(E2)]).

show_overlap_graphs(Overlap) -->
	{
	 findall(Nick,
		 (   rdf(Overlap, amalgame:member, M),
		     rdf(M, amalgame:nickname, literal(Nick), amalgame_nicknames)
		 ), Graphs),
	 sort(Graphs, Sorted),
	 atom_chars(Nicks, Sorted),
	 http_link_to_id(list_graph, [graph(Overlap)], Olink)
	},
	html([a([href(Olink)], Nicks)]).

show_alignments -->
	{
	 findall(Graph, is_alignment_graph(Graph,_), Graphs),
	 http_link_to_id(http_clear_cache, [], CacheLink),
	 http_link_to_id(http_compute_stats, [graph(all)], ComputeLink),
	 Note = ['These are cached results, ',
		 a([href(CacheLink)], 'clear cache'), ' or ',
		 a([href(ComputeLink)], 'compute all'), ' missing statistics.' ]
	},
	html([div([id(cachenote)], Note),
	      table([id(aligntable)],
		    [tr([
			 th('Abr'),
			 th('Source'),
			 th('# mapped'),
			 th('Target'),
			 th('# mapped'),
			 th('Format'),
			 th('# maps'),
			 th('Named Graph URI')

		       ]),
		    \show_alignments(Graphs,0)
		   ])
	     ]).

show_alignments([],Total) -->
	html(tr([id(finalrow)],
		[td(''),
		 td(''),
		 td(''),
		 td(''),
		 td(''),
		 td(''),
		 td([style('text-align: right')],Total),
		 td('Total (double counting)')
		])).

show_alignments([Graph|Tail], Number) -->
	{
	 http_link_to_id(http_compute_stats,
			 [graph(Graph),
			  stat(totalcount),
			  stat(source),
			  stat(target),
			  stat(mapped)
			 ],
			 MissingLink),
	 MissingValue = a([href(MissingLink)],'?'),
	 is_alignment_graph(Graph, Format),
	 align_get_computed_props(Graph, Props),
	 (   memberchk(count(literal(type(_,Count))), Props)
	 ->  NewNumber is Number + Count
	 ;   NewNumber = Number, Count = MissingValue
	 ),
	 (   memberchk(alignment(A), Props)
	 ->  http_link_to_id(list_resource, [r(A)], AlignLink),
	     FormatLink = a([href(AlignLink)], Format)
	 ;   FormatLink = Format
	 ),
	 (   memberchk(source(SourceGraph), Props)
	 ->  Source = \rdf_link(SourceGraph, [resource_format(label)])
	 ;   Source = MissingValue
	 ),
	 (   memberchk(target(TargetGraph), Props)
	 ->  Target = \rdf_link(TargetGraph, [resource_format(label)])
	 ;   Target = MissingValue
	 ),
	 (   memberchk(mappedSourceConcepts(MSC), Props)
	 ->  SourcesMapped = literal(type(_,MSC))
	 ;   SourcesMapped = MissingValue
	 ),
	 (   memberchk(mappedTargetConcepts(MTC), Props)
	 ->  TargetsMapped = literal(type(_,MTC))
	 ;   TargetsMapped = MissingValue
	 )
	},
	html(tr([
		 td(\show_alignment(Graph)),
		 td(Source),
		 td([style('text-align: right')],SourcesMapped),
		 td(Target),
		 td([style('text-align: right')],TargetsMapped),
		 td(FormatLink),
		 td([style('text-align: right')],Count),
		 td(\show_graph(Graph))
		])),
	show_alignments(Tail, NewNumber).

show_overlap -->
	{
	 find_overlap(CountList, [cached(Cached)]),
	 (   Cached
	 ->  http_link_to_id(http_clear_cache, [], CacheLink),
	     Note = ['These are results from the cache, ', a([href(CacheLink)], 'clear cache'), ' to recompute']
	 ;   Note = ''
	 )
	},
	html([
	      div([id(cachenote)], Note),
	      table([id(aligntable)],
		    [
		     tr([th('Overlap'),th('# maps'), th('Example')]),
		     \show_countlist(CountList,0)
		    ]
		  )
	     ]).

