:- module(align_stats,
	  [
	   % HTTP entry points:
	   http_list_alignments/1, % +Request
	   http_list_overlap/1,    % +Request
	   http_clear_cache/1      % +Request
	  ]).

:- use_module(library(http/http_dispatch)).
:- use_module(library(http/html_write)).
:- use_module(library(semweb/rdf_db)).
:- use_module('../compare/compare').

:- http_handler(amalgame(list_alignments),    http_list_alignments,     []).
:- http_handler(amalgame(find_overlap),       http_list_overlap,        []).
:- http_handler(amalgame(clear_cache),        http_clear_cache,         []).

%%	http_list_alignments(+Request) is det.
%
%	HTTP handler returning list of all alignments in HTML.

http_list_alignments(_Request) :-
	style(Style),
	reply_html_page(cliopatria(default),
			[title('Alignments'),
			 Style
			],
			[ h4('Alignments in the RDF store'),
			  \show_alignments
			]).


%%	http_list_overlap(+Request) is det.
%
%	HTTP handler generating a page with mapping overlap statistics.

http_list_overlap(_Request) :-
	style(Style),
	reply_html_page(cliopatria(default),
			[
			 title('Alignment overlap'),
			 Style
			],
			[
			 div([class(lfloat)],
			     [
			      h4('Alignments analyzed'),
			      \show_alignments
			     ]),
			 div([class(lfloat)],
			     [
			      h4('Alignment overlap'),
			      \show_overlap
			     ])
			]).

http_clear_cache(Request) :-
	clear_nicknames,
	clear_stats,
	http_list_alignments(Request).

%%	style(-Style) is det.
%
%	Common style for HTML pages generated by this module
%
style(style(
	    [type('text/css')],
	    ['#aligntable { padding: .3%; border: solid grey} ',
	     '#nicktable  { padding: .3%; border: dashed grey; margin-left: 5% } ',
	     '#totals td  { border-top: solid grey; font-weight: bold } ',
	     '#finalrow td { border-top: solid #AAAAAA; } ',
	     '.lfloat { float: left; margin-right: 2% }'
	    ])
     ).
